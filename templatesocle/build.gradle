plugins {
    id 'groovy'
    id 'java'
}

group 'lthil.sodifrance.fr'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

ext {
    jobDslVersion = '1.70'
    jenkinsVersion = '2.73'
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
    }
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    compile 'org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}'

    compile "org.jenkins-ci.main:jenkins-core:${jenkinsVersion}"

    //compile 'org.jenkins-ci.plugins:promoted-builds:2.28.1@jar'
    //compile group: 'org.jenkins-ci.plugins', name: 'promoted-builds', version: '2.28', ext:'jar'
    //compile group: 'org.jenkins-ci.plugins', name: 'job-dsl-promotions', version: '2.28', ext:'jar'

    compile 'javax.servlet:javax.servlet-api:3.1.0'

    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-job', version: '2.23', ext:'jar'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-api', version: '2.28', ext:'jar'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.33'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    testCompile "org.jenkins-ci.main:jenkins-core:${jenkinsVersion}"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.6@jar'

    // plugins to install in test instance
    //testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    //testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:promoted-builds:2.28.1@jar'
    testPlugins 'org.jenkins-ci.plugins:git:2.5.3'
    testPlugins 'org.jenkins-ci.plugins:envinject-api:1.2'
    testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.26'
    testPlugins 'org.jvnet.hudson.plugins:groovy-postbuild:2.2'

}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}
